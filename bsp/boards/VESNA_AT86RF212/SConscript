import os

Import('env')

localEnv = env.Clone()

# scons doesn't let us look to parent directories for source, so the
# bsp/chips/at86rf212/radio.c is off limits from this file. To keep things
# simple, each SConscript file in bsp/chips/* will return a list of objects
# which can be appended to the source list. Don't forget to specify a variant_dir,
# or else the build will occur directly in the chips directory.

rf212 = localEnv.SConscript(
    os.path.join('#','bsp','chips','at86rf212','SConscript'),
    variant_dir = 'rf212',
    exports     = {'env': env},
)

target   = 'libbsp'
bsp_dir  = os.path.join('#','bsp','boards','VESNA_AT86RF212')
source   = \
    Glob('*.c') + \
    Glob('*.s') + \
    Glob('configure/*.c') + \
    Glob('library/STM32F10x_StdPeriph_Lib_V3.5.0/Libraries/STM32F10x_StdPeriph_Driver/src/*.c') + \
    [rf212]


libbsp = localEnv.Library(
    target=target,
    source=source,
)

Alias('libbsp', libbsp)
